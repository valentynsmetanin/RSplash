plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.svapp.rsplash"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "api"

    productFlavors {
        prod {
            dimension "api"

            // TODO: Replace with production url.
            buildConfigField "String", "UNSPLASH_API_URL", "\"https://api.unsplash.com/\""
            buildConfigField "String", "UNSPLASH_ACCESS_KEY", getKey()['UNSPLASH_ACCESS_KEY_RELEASE']
        }
        dev {
            dimension "api"

            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"

            buildConfigField "String", "UNSPLASH_API_URL", "\"https://api.unsplash.com/\""
            buildConfigField "String", "UNSPLASH_ACCESS_KEY", getKey()['UNSPLASH_ACCESS_KEY_DEV']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.6.0-beta01'
    implementation 'com.google.android.material:material:1.7.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    def hiltVersion = "2.43.2"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'

    def navigationVersion = "2.5.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation 'com.google.code.gson:gson:2.9.1'

    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.10'

    def coilVersion = "2.1.0"
    implementation "io.coil-kt:coil:$coilVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

def getKey() {
    def keyFile = file("../key.properties")
    def secretKeys = new Properties()
    secretKeys.load(new FileInputStream(keyFile))
    return secretKeys
}